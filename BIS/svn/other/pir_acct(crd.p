{pirsavelog.p}

/* ------------------------------------------------------
File          : $RCSfile: pir_acct(crd.p,v $ $Revision: 1.4 $ $Date: 2007-10-18 07:42:21 $
Copyright     : ООО КБ "Пpоминвестрасчет"
Базируется    : acct(crd.p
Причина       : Списание с картотеки возможно только под вчерашний приход
Назначение    : Процедура просмотра и выбора картотечных счетов.
Место запуска : пранзакция списания с картотеки К2 070203
Автор         : Kuntash, Anisimov 
Изменения     : $Log: not supported by cvs2svn $
Изменения     : Revision 1.3  2007/09/21 09:49:07  lavrinenko
Изменения     : РЕализован следующий фильтр - отбираются только те картотечные счета на которые было зачисление в рейсе (подгруженном в текущий день) или при отсуствии рейса - сомтрятся зачисления из предыдцщего оп дня. @
Изменения     :
Изменения     : Revision 1.2  2007/09/14 12:14:41  lavrinenko
Изменения     : 1. Добавлен стандартный заголовок. 2. Реализовано корректное отпредедение поступлений в прошлых днях
Изменения     :
Изменения     : 05/02/2007 Кунташев В.Н. убрал блокировку счета
Изменения     : 03/05/2006 Anisimov      Учитываются блокировки счетов и неснижаемые остатки
------------------------------------------------------ */
/*
                Банковская интегрированная система БИСквит
    Copyright:  (C) 1992-1996 ТОО "Банковские информационные системы"
     Filename:  acct(crd.p
      Comment:  Просмотр и выбор внебалансовых счетов, работающих с указанным
                шаблоном КАУ. Входящий параметр - код шаблона кау. Исполь -
                зуется в процедуре списания с картотеки 2. Подключается через
                дополнительный реквизит шаблона проводки с кодом "ПроцКау".
                Счета выбираются по следующему алгоритму:
                  Выбираются все внебалансовые счета, работающие по указанному
                шаблону КАУ, за которые отвечает данный сотрудник или
                подчиненные ему сотрудники. На это накладываются по умолчанию
                ограничения - остаток на счете должен быть отличен от нуля ЗА
                эту дату и на соответствующем счете этого клиента на балансе
                НА дату должен быть остаток.
         Uses:  -
      Used by:  gcrddec.p
      Created:  15/09/1999 eagle
     modified: 08/05/2002 kostik  0006764 Формат балансового остатка, показ внебалансовых
                                          счетов с учетом прав пользователя
     modified: 03/05/2006 Anisimov  Учитываются блокировки счетов и неснижаемые остатки

*/
{globals.i}
{sh-defs.i}
{chkacces.i}
{intrface.get "acct"}
def input parameter in-code-value like code.code no-undo.

DEF VAR mOnlyUser    AS LOG  NO-UNDO.
DEF VAR mCurrentUser AS CHAR NO-UNDO.
DEF VAR mSum         AS DECIMAL INITIAL 0 no-undo.

mCurrentUser = userid('bisquit') + "," + getslaves().

&GLOB user-rights1 (   (    mOnlyUser                                 ~
                        AND LOOKUP(bfAcct.user-id,mCurrentUser) GT 0) ~
                    OR (    NOT mOnlyUser)                            ~
                    )

IF NUM-ENTRIES(in-code-value,"#") GT 1 THEN
   ASSIGN
      mOnlyUser = TRUE
      in-code-value = ENTRY(1,in-code-value,"#")
   .

def var vacct-cat   like acct.acct-cat no-undo.
def var long-acct   as char format "x(24)" no-undo.
def var comment_str as char label "                                   "  no-undo.
def var users       as char no-undo.
def var lstcont     as char initial "Расчет" no-undo.
def buffer bacct for acct.
def buffer bop-entry for op-entry.
def var dt-op   LIKE op-date.op-date INITIAL ? NO-UNDO.
def var summ-rr like op-entry.amt-rub no-undo.
def var vdebug as log init no no-undo.
def var ff-card as char no-undo.
DEF VAR name AS CHARACTER EXTENT 2 NO-UNDO.
DEF VAR Store-Position AS RECID NO-UNDO.
DEF TEMP-TABLE ttacct NO-UNDO
   FIELD acct        LIKE acct.acct
   FIELD acct-view   LIKE acct.acct  /* Номер счета в соответствующем формате */
   FIELD bacct       LIKE acct.acct
   FIELD bacct-view  LIKE acct.acct  /* Номер счета в соответствующем формате */
   FIELD curr        LIKE acct.curr
   FIELD name        AS   CHARACTER LABEL "Клиент"
                             FORMAT "x(40)"
   FIELD rec-oa      AS   RECID /* для ссылки на внебалансовый счет */
   FIELD obal        LIKE acct-pos.balance
   FIELD fobal       AS   LOG
   FIELD rec-ba      AS   RECID /* для ссылки на балансовый счет */
   FIELD bbal        LIKE acct-pos.balance
   FIELD fbbal       AS   LOG
   FIELD bbal-rub    LIKE acct-pos.balance /* для проверок на превышение лимита */
   FIELD bbal-val    LIKE acct-pos.balance
INDEX acct   IS PRIMARY acct curr bacct
INDEX wacct             fobal fbbal acct curr bacct
.

find code where code.class eq "ШаблКау"
            and code.code  eq in-code-value
                           no-lock no-error.
if not avail code then return.
{kautools.lib}
ff-card = FGetSetting("СтандТр", "findcard2", "Нет").

/* PIR BEGIN Определение даты загрузки поступлений из МЦИ */                                                                   
l-pack:                                                                               
    FOR EACH  Packet     WHERE Packet.PackDate         EQ gend-date   NO-LOCK,        
        FIRST PackObject WHERE PackObject.PacketID     EQ Packet.PacketID    AND      
                                  PackObject.kind      EQ 'ED211'            AND      
                                  PackObject.File-Name EQ 'op-entry' NO-LOCK,         
        FIRST bop-entry  WHERE bop-entry.op       EQ INT(ENTRY(1,PackObject.Surrogate)) AND 
                               bop-entry.op-entry EQ INT(ENTRY(2,PackObject.Surrogate))   
                               NO-LOCK:                                               
       dt-op = bop-entry.op-date.                                                     
       LEAVE l-pack.                                                                  
    END.                                                                              
                                                                                      
    IF dt-op EQ ? THEN DO:                                                            
       MESSAGE "Не обнаружена загрузка окончательной выписки из МЦИ !"                
         VIEW-AS ALERT-BOX TITLE "ВНИМАНИЕ !".  
       
       FIND LAST op-date WHERE op-date.op-date LT gend-date NO-LOCK NO-ERROR.
       dt-op = op-date.op-date.                                                         
    END.                                                                              
/* PIR END */                                                                         


&GLOB PROC-ACCT                                                                       ~
  RUN fdbacct( buffer acct, ff-card, in-code-value ).                                 ~
  FOR EACH buf-ttKau WHERE buf-ttKau.fTbName EQ "ACCTB" NO-LOCK,                      ~
    FIRST bacct WHERE RECID(bacct) EQ buf-ttKau.fRecId 	NO-LOCK,                      ~
    /* PIR BEGIN */                                                                   ~
    FIRST bop-entry WHERE bop-entry.acct-cr EQ bacct.acct                             ~
                      AND bop-entry.op-date EQ dt-op  NO-LOCK  /* PIR END */          ~
    BREAK BY bacct.acct :                                                             ~
      CREATE ttacct.                                                                  ~
      ASSIGN                                                                          ~
        ttacct.acct-view   = STRING(acct.acct,GetAcctFmt(code.misc[8]))               ~
        ttacct.rec-oa      = recid(acct)                                              ~
        ttacct.acct        = acct.acct                                                ~
        ttacct.curr        = acct.curr                                                ~
      .                                                                               ~
      RUN acct-pos IN h_base (ttacct.acct,                                            ~
                              ttacct.curr,                                            ~
                              gend-date,                                              ~
                              gend-date,                                              ~
                              "П").                                                   ~
      ASSIGN                                                                          ~
        ttacct.obal  = IF ttacct.curr > "" THEN sh-val                                ~
                                            ELSE sh-bal                               ~
        ttacct.fobal = ttacct.obal NE 0                                               ~
      .                                                                               ~
      ASSIGN                                                                          ~
          ttacct.rec-ba = recid(bacct)                                                ~
      .                                                                               ~
      RUN acct-pos IN h_base (bacct.acct,                                             ~
                              bacct.curr,                                             ~
                              gend-date,                                              ~
                              gend-date,                                              ~
                              "П").                                                   ~
/* PIR BEGIN */                                                                       ~
      mSum = 0.                                                                       ~
      FOR EACH loan-acct WHERE loan-acct.acct = bacct.acct NO-LOCK,                   ~
          LAST term-obl WHERE term-obl.cont-code = loan-acct.cont-code AND            ~
                                  ( (term-obl.idnt = 2) OR (term-obl.idnt = 22) ) AND ~
                                  term-obl.contract EQ loan-acct.acct-type NO-LOCK:   ~
               mSum = mSum +  term-obl.amt-rub.                                       ~
      END. /* PIR END */                                                              ~
      ASSIGN                                                                          ~
          ttacct.bacct-view = STRING(bacct.acct,GetAcctFmt(code.misc[8]))             ~
          ttacct.bacct      = bacct.acct                                              ~
          ttacct.rec-ba     = RECID(bacct)                                            ~
          ttacct.name       = IF acct.cust-cat EQ "Ю" THEN (name[1] + " " + name[2])  ~
                                                  ELSE (name[1] +  " " + name[2])     ~
          ttacct.bbal       = IF bacct.curr  GT "" THEN sh-val ELSE sh-bal            ~
          ttacct.fobal      = IF ttacct.obal GT 0  THEN yes    ELSE no                ~
/* PIR */ ttacct.fbbal      = IF (ttacct.bbal - mSum) LT 0  THEN yes    ELSE no       ~
          ttacct.bbal-rub   = sh-bal                                                  ~
          ttacct.bbal-val   = sh-val                                                  ~
      .                                                                               ~
  END.


RUN SelectAcctOfKauId(code.code).
/* Заполнение временной таблицы */
FOR EACH ttKau WHERE ttKau.fTbName EQ "ACCT" NO-LOCK,
    FIRST acct WHERE RECID(acct) EQ ttKau.fRecId NO-LOCK:
   {getcust.i &name=name &Offinn="/*"}
   {&PROC-ACCT}
END.


FORM
   ttacct.acct-view FORMAT "x(25)"
               HELP "Внебалансовый счет"
               SPACE(5)
   ttacct.obal COLUMN-LABEL "ОСТАТОК НА!ВНЕБАЛАНСОВОМ СЧЕТЕ"
               HELP "Остаток на внебалансовом счете"
   ttacct.bbal COLUMN-LABEL "ОСТАТОК НА!БАЛАНСОВОМ СЧЕТЕ"
               HELP "Остаток на балансовом счете"
WITH FRAME BROWSE1
     TITLE COLOR BRIGHT-WHITE "[ ЛИЦЕВЫЕ СЧЕТА ПО СОСТОЯНИЮ НА " + STRING(gend-date) + " ]"
     WIDTH 79.

FORM
   ttacct.acct-view  FORMAT "x(25)"
                HELP "Внебалансовый счет"
   ttacct.bacct-view FORMAT "x(25)"
                COLUMN-LABEL  "!СЧЕТ НА БАЛАНСЕ"
                HELP "Балансовый счет"
   ttacct.obal  COLUMN-LABEL "ОСТАТОК НА!ВНЕБАЛАНСОВОМ СЧЕТЕ"
                HELP "Остаток на внебалансовом счете"
WITH FRAME BROWSE2
     TITLE COLOR BRIGHT-WHITE "[ ЛИЦЕВЫЕ СЧЕТА ПО СОСТОЯНИЮ НА " + STRING(gend-date) + " ]"
     WIDTH 79.

FORM
   ttacct.acct-view  FORMAT "x(25)"
                HELP "Внебалансовый счет"
   ttacct.name  FORMAT "x(47)"
                COLUMN-LABEL "!НАИМЕНОВАНИЕ СЧЕТА"
                HELP "Наименование (владельца) лицевого счета"
WITH FRAME BROWSE3
     TITLE COLOR BRIGHT-WHITE "[ ЛИЦЕВЫЕ СЧЕТА ]"
     WIDTH 79.


&glob oqry0 open query qry0 for each ttacct where ttacct.fobal and ttacct.fbbal no-lock.
&glob oqry1 open query qry0 for each ttacct where ttacct.fobal no-lock.
&glob oqry2 open query qry0 for each ttacct no-lock.

release ttacct.

{navigate.cqr
   &file     = ttacct
   &files    = "ttacct"
   &qry      = "qry0"
   &maxoq    = 3
   &avfile   = "ttacct "
   &defquery = "def query qry0 for ttacct scrolling."
   &maxfrm   = 3
   &bf1      = "ttacct.acct-view ttacct.obal ttacct.bbal "
   &bf2      = "ttacct.acct-view ttacct.bacct-view ttacct.obal "
   &bf3      = "ttacct.acct-view ttacct.name "
   &workfile = "/*"
   &nodel    = "/*"
   &look     = "acct(crd.nav "
   &return   = "acct(crd.ret "
   &oh3      = "│F3"
   &oth3     = "acct(crd.f3 "
   &oh6      = "│F6"
   &oth6     = "acct(crd.f6 "
   &oh2      = "│F2-Детализация"
   &oth2     = "acct(crd.f2 "
   &oh7      = "│F7"
   &oth7     = "findsp.cqr "
     &find1  = "searchsp.cqr  &file-name = ttacct   ~
                              &sfld      = acct     ~
                              &metod     = matches  ~
               "             
     &find2  = "searchsp.cqr  &file-name = ttacct   ~
                              &sfld      = bacct    ~
                              &metod     = matches  ~
               "             
     &find3  = "searchsp.cqr  &file-name = ttacct   ~
                              &sfld      = name     ~
                              &metod     = matches  ~
                              &metmatch  = YES      ~
               "             

}

/*
   &n-str=num-line

   &oh2="│F3 форма"
   &oth2="op-frm.chg "
*/
{intrface.del "acct"}
RETURN.
