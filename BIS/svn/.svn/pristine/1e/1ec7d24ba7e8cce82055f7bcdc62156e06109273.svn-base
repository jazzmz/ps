/*
               Банковская интегрированная система БИСквит
    Copyright: (C) 1992-1997 ТОО "Банковские информационные системы"
     Filename: ovn-nps.p
      Comment: объявление на взнос наличными
               (Открытая система печати)
      Comment:
   Parameters: 
         Uses: 
      Used by:
      Created: 23/09/2004 kraw (0026941)
     Modified: 15/01/2008 kraw (0086034) Сумма цифрами без "="
     Modified: 16/12/2011 kraw (0161338) Восстановили имя и ИНН получателя.
     Modified: 21/02/2012 kraw (0165983) в LAW_318p убираем ИНН из имени
*/
{globals.i}                                 /* глобальные переменные         */

DEFINE INPUT PARAMETER iRID AS RECID NO-UNDO.

{prn-doc.def &with_proc=YES}
{sumstrfm.i}
&IF DEFINED(LAW_318p) <> 0 &THEN
   {wordwrap.def}
&ENDIF
{intrface.get cust}

DEFINE VARIABLE mPName  AS CHARACTER EXTENT  2 NO-UNDO.

DEFINE VARIABLE mAmt1     AS CHARACTER NO-UNDO.
DEFINE VARIABLE mchAmt    AS CHARACTER NO-UNDO.
DEFINE VARIABLE mchDec    AS CHARACTER NO-UNDO.
DEFINE VARIABLE mAmtStr   AS CHARACTER NO-UNDO.
DEFINE VARIABLE mDDate    AS CHARACTER NO-UNDO.
DEFINE VARIABLE mStr      AS CHARACTER NO-UNDO.
DEFINE VARIABLE mStrTMP   AS CHARACTER NO-UNDO.
DEFINE VARIABLE mStrTMP1V AS CHARACTER EXTENT 2 NO-UNDO.
DEFINE VARIABLE mStrTMP2V AS CHARACTER EXTENT 2 NO-UNDO.
DEFINE VARIABLE mItem     AS INT64 INITIAL 1   NO-UNDO.
DEFINE VARIABLE mAmt      AS DECIMAL INITIAL 0.0 NO-UNDO.
DEFINE VARIABLE mBankName AS CHARACTER NO-UNDO.
DEFINE VARIABLE mPlName   AS CHARACTER NO-UNDO.
DEFINE VARIABLE mSumFormat AS CHARACTER NO-UNDO.
DEFINE VARIABLE mSumSep    AS CHARACTER NO-UNDO.
DEFINE VARIABLE mIsINN2    AS LOGICAL   NO-UNDO.

&IF DEFINED(LAW_318p) <> 0 &THEN
   DEFINE VARIABLE mPlNameP  AS CHARACTER EXTENT 4 NO-UNDO.
   DEFINE VARIABLE mPoName     AS CHARACTER NO-UNDO.
   DEFINE VARIABLE mPoINN      AS CHARACTER NO-UNDO.
   DEFINE VARIABLE mPoKPP      AS CHARACTER NO-UNDO.
   DEFINE VARIABLE mPoOKATO    AS CHARACTER NO-UNDO.
   DEFINE VARIABLE mPoRAcct    AS CHARACTER NO-UNDO.
   DEFINE VARIABLE mPoBankName AS CHARACTER NO-UNDO.
   DEFINE VARIABLE mPoBankCode AS CHARACTER NO-UNDO.
&ENDIF
DEFINE BUFFER recacct FOR acct.

RUN GetSeparator318p(OUTPUT mSumSep).

FIND FIRST op WHERE RECID(op) = iRID NO-LOCK NO-ERROR.

IF NOT AVAILABLE op THEN
DO:
  MESSAGE "Нет записи <op>".
  RETURN.
END.

FOR EACH op-entry OF op NO-LOCK:

   &IF DEFINED(LAW_318p) <> 0 &THEN
      IF mItem GT 4 THEN
         LEAVE.
   &ELSE
      IF mItem GT 10 THEN
         LEAVE.
   &ENDIF

   ACCUMULATE op-entry.amt-rub (TOTAL).

   RUN Insert_TTName("Symb" + STRING(mItem,"9"), op-entry.symbol).

   mSumFormat = getFormatStr(11, ",", YES).
   mStrTmp    = AmtStrSepFormat(op-entry.amt-rub, mSumSep, mSumFormat).

   RUN Insert_TTName("Sum" + STRING(mItem,"9"),
                     IF INT64(op-entry.amt-rub) = op-entry.amt-rub THEN
                         (STRING(op-entry.amt-rub, "-zzzzzzzzzzzz9") + "=")
                     ELSE
                         mStrTmp).
   &IF DEFINED(LAW_318p) = 0 &THEN
   RUN Insert_TTName("Sum0" + STRING(mItem,"9"), mStrTmp).
   &ELSE
   RUN Insert_TTName("Sum0" + STRING(mItem,"9"), TRIM(mStrTmp)).
   &ENDIF
   mItem = mItem + 1.
END.

mAmt = ACCUM TOTAL op-entry.amt-rub.

FIND FIRST op-entry OF op NO-LOCK NO-ERROR.

mDDate = IF op.doc-date NE ? THEN {strdate.i op.doc-date}
                             ELSE {strdate.i op.op-date}.
mDDate = REPLACE(mDDate, " г.", "").

&IF DEFINED(LAW_318p) <> 0 &THEN
   RUN Insert_TTName("d-date", mDDate + " года").
   IF NUM-ENTRIES(mDDate," ") >= 3 THEN DO:
      RUN Insert_TTName("d-date-day", ENTRY(1,mDDate," ")).
      RUN Insert_TTName("d-date-month", ENTRY(2,mDDate," ")).
      RUN Insert_TTName("d-date-year", SUBSTR(ENTRY(3,mDDate," "),3)).
   END.
&ELSE
   RUN Insert_TTName("d-date", mDDate).
&ENDIF

RUN Insert_TTName("Detail", op.detail).

IF op-entry.acct-cr NE ?  THEN
   RUN Insert_TTName("AcctCr", DelFilFromAcct(op-entry.acct-cr)).

{find-act.i &acct=op-entry.acct-cr &curr=op-entry.currency}

IF {assigned op.name-ben} THEN
   mPlName = op.name-ben.
ELSE
DO:
   IF acct.cust-cat = 'В' THEN
   DO:
      RUN Insert_TTName("PlNameINN", 'ИНН ' + FGetSetting("ИНН", "", "") + ' ' 
                                     + FGetSetting("Банк","","")).
      RUN Insert_TTName("PlNameWOINN", FGetSetting("Банк","","")).
      mPlName = (IF FGetSetting("ПлатДок", "ВыводИНН", "Да") EQ "Да" THEN
                ('ИНН ' + FGetSetting("ИНН", "", "") + ' ') ELSE "" )
              + FGetSetting("Банк","","").
   END.
   ELSE IF acct.cust-cat = 'Ю' THEN
   DO:
      IF AVAIL acct THEN DO:
         RUN GetCustInfo2 (13, acct.acct, acct.currency, OUTPUT mStrTMP1V[1]).
         RUN GetCustNameFormatted (acct.cust-cat, acct.cust-id, OUTPUT mStrTMP1V[2]).
      END.  
      ELSE ASSIGN
         mStrTMP1V = ""
         mStrTMP2V = ""
      .
      RUN Insert_TTName("PlNameINN", mStrTMP1V[1] + " " + mStrTMP1V[2]).
      RUN Insert_TTName("PlNameWOINN", mStrTMP1V[2]).
      IF FGetSetting("ПлатДок", "ВыводИНН", "Да") EQ "Да" THEN
         mPlName = mStrTMP1V[1] + " " + mStrTMP1V[2].
      ELSE
         mPlName = mStrTMP1V[2].
   END.
   ELSE
   DO:
      IF AVAIL acct THEN DO:
         {getcust.i &name=mStrTMP1V}
         {getcust.i &name=mStrTMP2V &OFFInn=YES}
      END.
      ELSE ASSIGN
         mStrTMP1V = ""
         mStrTMP2V = ""
      .

      RUN Insert_TTName("PlNameINN", mStrTMP1V[1] + " " + mStrTMP1V[2]).
      RUN Insert_TTName("PlNameWOINN", mStrTMP2V[1] + " " + mStrTMP2V[2]).
      IF FGetSetting("ПлатДок", "ВыводИНН", "Да") EQ "Да" THEN
         mPlName = mStrTMP1V[1] + " " + mStrTMP1V[2].
      ELSE
         mPlName = mStrTMP2V[1] + " " + mStrTMP2V[2].
   END.
END.

&IF DEFINED(LAW_318p) <> 0 &THEN
   mPlNameP = "От кого " + mPlName.
   {wordwrap.i &s=mPlNameP &n=4 &l=37}
   IF mPlNameP[4] = "" THEN DO:
      /* 1. aaa    ___       1. aaa    ___       1. aaa    ___
         2. bbb => aaa  или  2. bbb => aaa  или  2. ___ => aaa
         3. ccc    bbb       3. ___    bbb       3. ___    ___
         4. ___    ccc       4. ___    ___       4. ___    ___
      */
      ASSIGN
         mPlNameP[4] = mPlNameP[3]
         mPlNameP[3] = mPlNameP[2]
         mPlNameP[2] = mPlNameP[1]
         mPlNameP[1] = ""
      .
      /*                     1. ___    ___       1. ___    ___
                             2. aaa => ___  или  2. aaa => ___
                             3. bbb    aaa       3. ___    aaa
                             4. ___    bbb       4. ___    ___
      */
      IF mPlNameP[4] = "" THEN ASSIGN
         mPlNameP[4] = mPlNameP[3]
         mPlNameP[3] = mPlNameP[2]
         mPlNameP[2] = ""
      .
   END.
   RUN Insert_TTName("PlName11", mPlNameP[1]).
   RUN Insert_TTName("PlName12", mPlNameP[2]).
   RUN Insert_TTName("PlName13", mPlNameP[3]).
   RUN Insert_TTName("PlName14", mPlNameP[4]).

   mPlNameP = "От кого " + mPlName.
   {wordwrap.i &s=mPlNameP &n=3 &l=62}
   IF mPlNameP[3] = "" THEN DO:
      /* 1. aaa    ___  или  1. aaa    ___
         2. bbb => aaa       2. ___ => aaa
         3. ___    bbb       3. ___    ___
      */
      ASSIGN
         mPlNameP[3] = mPlNameP[2]
         mPlNameP[2] = mPlNameP[1]
         mPlNameP[1] = ""
      .
      /* 1. ___    ___
         2. aaa => ___
         3. ___    aaa
      */
      IF mPlNameP[3] = "" THEN ASSIGN
         mPlNameP[3] = mPlNameP[2]
         mPlNameP[2] = ""
      .
   END.
   RUN Insert_TTName("PlName21", mPlNameP[1]).
   RUN Insert_TTName("PlName22", mPlNameP[2]).
   RUN Insert_TTName("PlName23", mPlNameP[3]).
&ENDIF
RUN Insert_TTName("PlName", mPlName).
mIsINN2 = NO.

IF acct.cust-cat EQ 'В' THEN
DO:
   &IF DEFINED(LAW_318p) <> 0 &THEN
      mPoINN = FGetSetting("ИНН", "", "").
      mPoKPP = FGetSetting("БанкКПП", "", "").
      FOR FIRST branch WHERE branch.branch-id = acct.branch-id NO-LOCK:
         mPoOKATO = GetXAttrValueEx("branch",acct.branch-id,"ОКАТО-НАЛОГ","").
      END.
      IF NOT {assigned mPoOKATO}
      THEN mPoOKATO = FGetSetting("БанкОКАТО", "", "").
      mPoName = FGetSetting("Банк","", "").
   &ELSE
      RUN Insert_TTName("PoNameINN", "ИНН " + FGetSetting("ИНН", "", "") + " "
                                     + FGetSetting("Банк","","")).
      RUN Insert_TTName("PoNameWOINN", FGetSetting("Банк","","")).
      RUN Insert_TTName("PoName", (IF FGetSetting("ПлатДок", "ВыводИНН", "Да") EQ "Да" THEN
                                     ('ИНН ' + FGetSetting("ИНН", "", "") + ' ') ELSE "" )
                                  + FGetSetting("Банк","","")).
   &ENDIF
END.
ELSE
DO:
   IF AVAIL acct THEN DO:
      IF acct.cust-cat EQ "Ю" THEN DO:
         &IF DEFINED(LAW_318p) <> 0 &THEN
            IF CAN-DO(FGetSetting("ПлатДок", "ПолучОбъяв", ""),STRING(acct.bal-acct)) THEN DO:
               {getcust.i &name=mStrTMP2V &OFFInn=YES &OFFsigns=YES &inn=mStrTMP}
            END.
            ELSE DO: 
               {getcust.i &name=mStrTMP2V &OFFInn=YES &inn=mStrTMP}
            END.
         &ELSE
            {getcust.i &name=mStrTMP2V &OFFInn=YES}
         &ENDIF
         RUN GetCustInfo2 (13, acct.acct, acct.currency, OUTPUT mStrTMP2V[1]).
         mIsINN2 = YES.
         RUN GetCustNameFormatted (acct.cust-cat, acct.cust-id, OUTPUT mStrTMP2V[2]).
      END.
      ELSE DO:
         {getcust.i &name=mStrTMP1V}
         &IF DEFINED(LAW_318p) <> 0 &THEN
            IF CAN-DO(FGetSetting("ПлатДок", "ПолучОбъяв", ""),STRING(acct.bal-acct)) THEN DO:
               {getcust.i &name=mStrTMP2V &OFFInn=YES &OFFsigns=YES &inn=mStrTMP}
            END.
            ELSE DO: 
               {getcust.i &name=mStrTMP2V &OFFInn=YES &inn=mStrTMP}

               IF CAN-DO(FGetSetting("БалСчИНН","",""), STRING(acct.bal-acct)) THEN DO:

                  CASE acct.cust-cat:
                     WHEN "Ч" THEN DO:
                        FIND FIRST person WHERE person.person-id = acct.cust-id NO-LOCK NO-ERROR.
                        IF AVAIL person THEN 
                           mStrTMP = person.inn.
                     END.
                     WHEN "Б" THEN DO:
                        FIND FIRST banks WHERE banks.bank-id = acct.cust-id NO-LOCK NO-ERROR.
                        IF AVAIL banks THEN DO:
                           mStrTMP = GetBankInn ("bank-id", STRING (banks.bank-id)).
                        END.
                     END.
                  END CASE.
                  IF mStrTmp EQ ? THEN mStrTMP = "".
               END.

            END.
         &ELSE
            {getcust.i &name=mStrTMP2V &OFFInn=YES}
         &ENDIF
      END.

      IF mStrTMP NE "" THEN
         ASSIGN
            mStrTMP1V[1] = "ИНН " + mStrTMP
            mStrTMP1V[2] = mStrTMP2V[1] + " " + mStrTMP2V[2]
         .
   END.
   ELSE ASSIGN
      mStrTMP1V = ""
      mStrTMP2V = ""
   .

   &IF DEFINED(LAW_318p) <> 0 &THEN

      IF mIsINN2 THEN
         mStrTMP2V[1] = "".

      mPoName = TRIM(mStrTMP2V[1] + " " + mStrTMP2V[2]).
      mPoINN = mStrTMP.
      CASE acct.cust-cat:
         WHEN "Ю" THEN mStrTMP = "cust-corp".
         WHEN "Ч" THEN mStrTMP = "person".
         WHEN "Б" THEN mStrTMP = "banks".
      END CASE.
      mPoKPP = GetXAttrValueEx(mStrTMP, STRING(acct.cust-id), "КПП", "").
      mPoOKATO = GetXAttrValueEx(mStrTMP, STRING(acct.cust-id), "ОКАТО-НАЛОГ", "").
      FIND FIRST recacct WHERE recacct.cust-cat = acct.cust-cat
                           AND recacct.cust-id  = acct.cust-id
                           AND recacct.acct-cat = "b"
                           AND recacct.contract = "Расчет"
                           AND CAN-FIND(FIRST op-entry OF op WHERE op-entry.acct-db = recacct.acct
                                                                OR op-entry.acct-cr = recacct.acct)
      NO-LOCK NO-ERROR.
      IF NOT AVAIL recacct
         THEN FIND FIRST recacct WHERE recacct.cust-cat   = acct.cust-cat
                                   AND recacct.cust-id    = acct.cust-id
                                   AND recacct.acct-cat   = "b"
                                   AND recacct.contract   = "Расчет"
                                   AND recacct.open-date <= op.op-date
                                   AND (   recacct.close-date = ?
                                        OR recacct.close-date > op.op-date)
         NO-LOCK NO-ERROR.
      IF  AVAIL recacct
      AND {assigned recacct.number}
      THEN mPoRAcct = recacct.number.
   &ELSE
      IF acct.cust-cat EQ "Ю" THEN DO:
         RUN Insert_TTName("PoNameINN", mStrTMP1V[1] + " " + mStrTMP1V[2]).
         RUN Insert_TTName("PoNameWOINN", mStrTMP1V[2]).

         IF FGetSetting("ПлатДок", "ВыводИНН", "Да") EQ "Да" THEN
            RUN Insert_TTName("PoName", mStrTMP1V[1] + " " + mStrTMP1V[2]).
         ELSE
            RUN Insert_TTName("PoName", mStrTMP1V[2]).
      END.
      ELSE DO:
         RUN Insert_TTName("PoNameINN", mStrTMP1V[1] + " " + mStrTMP1V[2]).
         RUN Insert_TTName("PoNameWOINN", mStrTMP2V[1] + " " + mStrTMP2V[2]).

         IF FGetSetting("ПлатДок", "ВыводИНН", "Да") EQ "Да" THEN
            RUN Insert_TTName("PoName", mStrTMP1V[1] + " " + mStrTMP1V[2]).
         ELSE
            RUN Insert_TTName("PoName", mStrTMP2V[1] + " " + mStrTMP2V[2]).
      END. 
   &ENDIF

END.

mBankName = dept.name-bank.
&IF DEFINED(LAW_318p) <> 0 &THEN
   IF FGetSetting("ПлатДок", "ВыводМест", "Нет") = "Да" THEN DO:
      mBankName = mBankName + ", " + FGetSetting("БанкГород",?,"").
   END.
&ENDIF
RUN Insert_TTName("NameBank", mBankName).
&IF DEFINED(LAW_318p) <> 0 &THEN
   /* Реквизиты документа перекрывают всё */
   mStr = GetXAttrValueEx("op", STRING(op.op), "name-rec", "").
   IF {assigned mStr} THEN DO:
      mPoName  = mStr.
      mPoINN   = GetXAttrValueEx("op", STRING(op.op), "INN-rec", "").
      mPoKPP   = GetXAttrValueEx("op", STRING(op.op), "KPP-rec", "").
      mPoOKATO = GetXAttrValueEx("op", STRING(op.op), "OKATO-rec", "").
   END.

   IF   {assigned op.ben-acct}
   THEN mPoRAcct = op.ben-acct.

   FIND op-bank OF op NO-LOCK NO-ERROR.
   IF AMBIGUOUS op-bank
   THEN FIND op-bank OF op WHERE op-bank.op-bank-type = "" NO-LOCK NO-ERROR.
   IF AVAIL op-bank THEN ASSIGN
      mPoBankName = op-bank.bank-name
      mPoBankCode = op-bank.bank-code
   .
   ELSE ASSIGN
      mPoBankName = mBankName
      mPoBankCode = GetXAttrValueEx("branch",dept.branch,"БанкМФО","")
   .

   /* Заполнение раквизитов в шаблон */
   RUN Insert_TTName("PoName", mPoName).
   IF {assigned mPoINN}
   THEN RUN Insert_TTName("PoINN", mPoINN).
   ELSE RUN Insert_TTName("PoINN", FILL(CHR(255),5) + FILL("─",8)).
   IF {assigned mPoKPP}
   THEN RUN Insert_TTName("PoKPP", mPoKPP).
   ELSE RUN Insert_TTName("PoKPP", FILL(CHR(255),4) + FILL("─",5)).
   IF {assigned mPoOKATO}
   THEN RUN Insert_TTName("PoOKATO", mPoOKATO).
   ELSE RUN Insert_TTName("PoOKATO", FILL(CHR(255),4) + FILL("─",5)).

   IF {assigned mPoRAcct}
   THEN do:
/*	RUN Insert_TTName("PoRAcct", mPoRAcct). #2892 Никитина Ю.А. 22.04.2013 */
	IF op-entry.acct-cr NE ?  THEN RUN Insert_TTName("PoRAcct", DelFilFromAcct(op-entry.acct-cr)). /* #2892 Никитина Ю.А. 22.04.2013*/
   end.
   ELSE do:
	RUN Insert_TTName("PoRAcct", FILL(CHR(255),11) + FILL("─",20)).
   end.

   RUN Insert_TTName("Pobank-MFO", mPoBankCode).
   RUN Insert_TTName("PoNameBank", mPoBankName).

   RUN Insert_TTName("bank-MFO", GetXAttrValueEx("branch",dept.branch,"БанкМФО","")).
&ELSE
   RUN Insert_TTName("bank-MFO", FGetSetting("БанкМФО", ?, "")).
&ENDIF

mSumFormat = REPLACE(getFormatStr(11, ",", YES),"z", ">").
mStrTmp = TRIM(AmtStrSepFormat(mAmt, mSumSep, mSumFormat)).

RUN Insert_TTName("Amt0", mStrtMP).
RUN Insert_TTName("Amt1",
                  IF INT64(mAmt) = mAmt THEN
                      (TRIM(STRING(mAmt, "->>>>>>>>>>>>9")) + "=")
                  ELSE
                      mStrTmp).
mAmt1 = FILL(CHR(205), 24 - LENGTH(mAmt1)) + mAmt1.
RUN Insert_TTName("Amt1_1", mAmt1).
RUN Insert_TTName("Amt01", mStrTmp).
mAmt1 = FILL(CHR(205), 24 - LENGTH(mAmt1)) + mAmt1.
RUN Insert_TTName("Amt01_1", mAmt1).

&IF DEFINED(LAW_318p) <> 0 &THEN
   RUN x-amtstr.p (mAmt,
                   "",
                   NO,
                   NO,
                   OUTPUT mchAmt, OUTPUT mchDec).
   RUN Insert_TTName("AmtStrRub", mchAmt).
   RUN Insert_TTName("AmtStrKop", mchDec).
   mAmtStr = mchAmt + IF INT64(mAmt) NE mAmt THEN (" " + mchDec) ELSE "".
&ELSE
   RUN x-amtstr.p (mAmt,
                   "",
                   YES,
                   YES,
                   OUTPUT mchAmt, OUTPUT mchDec).
   mAmtStr = mchAmt + IF INT64(mAmt) NE mAmt THEN (" " + mchDec) ELSE "".
&ENDIF

RUN Insert_TTName("AmtStr", mAmtStr).
RUN Insert_TTName("DocNum", op.doc-num).
RUN Insert_TTName("AcctDb", DelFilFromAcct(op-entry.acct-db)).

&IF DEFINED(LAW_318p) <> 0 &THEN
   RUN printvd.p({&vidDoc}, INPUT TABLE ttnames).
&ELSE
   RUN printvd.p("ОНВН", INPUT TABLE ttnames).
&ENDIF

{intrface.del}
