/*
               Банковская интегрированная система БИСквит
    Copyright: (C) 1992-1998 ТОО "Банковские информационные системы"
     Filename: op-ed.p
      Comment: Редактирование документа
   Parameters:
         Uses:
      Used by:
      Created: 17.08.1999 Peter
     Modified: 24.10.2000 Om Добавление поля op.ins-date - "Дата поступления"
     Modified: 03/12/2001 NIK Вызов kautrig.p заменен на  Kau-Trigger in h_op.
     Modified: 15.11.2002 Gunk Длина счета
     Modified: 17/06/2003 Gunk Еще раз длина счета
     Modified: 08/06/2004 kraw (0029512) Если через "банк-клиент" было введено без МФО
     Modified: 27/08/2007 kraw (0077186) блокировка счетов клиентов (запрет резактирования)
     Modified: 12/12/2007 kraw (0085886) более информативное сообщение о блокировке счетов клиентов 
     Modified: 03.06.2008 14:05 KSV      (0085464) Незнач. исправление     
*/

{globals.i}
{defopupd.i}
{intrface.get xclass}
{chkopmf.i}

&GLOBAL-DEFINE rec-label  "БАНК-ПОЛУЧАТЕЛЬ"
&GLOBAL-DEFINE send-label "БАНК-ОТПРАВИТЕЛЬ"
&GLOBAL-DEFINE ben-format 2
&GLOBAL-DEFINE mBankCodeType mBankCodeType

DEFINE INPUT PARAMETER in-rec-id  AS RECID   NO-UNDO.
DEFINE INPUT PARAMETER in-op-date AS DATE    NO-UNDO.
DEFINE INPUT PARAMETER in-user-id LIKE op-entry.user-id .
DEFINE INPUT PARAMETER level      AS INT64 NO-UNDO.

DEFINE BUFFER bank1 FOR banks.
DEFINE BUFFER bank2 FOR banks.
DEFINE BUFFER xop   FOR op.

DEFINE VARIABLE vmfo          LIKE op-bank.bank-code                 NO-UNDO.
DEFINE VARIABLE vcorr-acct    LIKE op-bank.corr-acct FORMAT "x(35)"  NO-UNDO.
DEFINE VARIABLE mci-reference LIKE op-bank.corr-acct                 NO-UNDO.

DEFINE VARIABLE dockind AS CHARACTER INITIAL {&rec-label} NO-UNDO.
DEFINE VARIABLE flager  AS INT64                        NO-UNDO.

DEFINE VARIABLE flag-error  AS INT64                        NO-UNDO.

DEFINE VARIABLE mIsVMFO_F1 AS LOGICAL INITIAL NO NO-UNDO.
DEFINE VARIABLE hproc      AS HANDLE             NO-UNDO.
DEFINE VARIABLE h_frame    AS HANDLE             NO-UNDO.

DEFINE VARIABLE mMfoOriginal  AS CHARACTER NO-UNDO.
DEFINE VARIABLE mBankNameOrig AS CHARACTER NO-UNDO.

{intrface.get cmp}
{intrface.get xclass}
{intrface.get op}
{intrface.get acct}
{intrface.get tmess}
{intrface.get rights}   /* Библиотека для работы с правами и паролями. */
{op-ed.frm edit}
{op.trg &FRAME-NAME=edit}
{g-error.def}
{intrface.get cust}



FUNCTION g-checkbank RETURNS LOGICAL (INPUT vmfo       AS CHARACTER,
                                      INPUT iCodeType  AS CHARACTER,
                                      INPUT vcorr-acct AS CHARACTER,
                                      INPUT benacct    AS CHARACTER,
                                      OUTPUT RESULT    AS INT64,
                                      OUTPUT msg       AS CHARACTER)
   IN hproc.

PROCEDURE CheckBenAcct :

   DEFINE INPUT  PARAM iBenAcct  AS CHAR NO-UNDO.
   DEFINE INPUT  PARAM iVMFO     AS CHAR NO-UNDO.
   DEFINE INPUT  PARAM iCorrAcct AS CHAR NO-UNDO.
   DEFINE OUTPUT PARAM oOk       AS LOG  NO-UNDO.

   DEFINE VARIABLE vBenLen AS INT64    NO-UNDO.
   DEFINE VARIABLE vLen    AS CHARACTER  NO-UNDO.


   ASSIGN
      oOk     = YES
      vBenLen = LENGTH (STRING (" ",GetCodeMisc ("КодБанка", mBankCodeType, {&ben-format})))
   NO-ERROR.

   IF ERROR-STATUS:ERROR OR vBenLen EQ ? THEN
   DO:
      vBenLen = 20. /* По умолчанию */
      /* Ругань? */
   END.
   DO WITH FRAME edit:
      IF iBenAcct NE "" AND LENGTH (iBenAcct) NE vBenLen THEN
      DO:
         pick-value = "NO".
         {message1
            &text    = "|Неверная длина Р/с|Продолжить?"
            &buttons = YES-NO
         }
         IF pick-value EQ "NO" THEN oOk = NO.
      END.

      /* Ключ проверяем только для российских счетов */
      IF oOk AND vBenLen LE 20 AND mBankCodeType EQ 'МФО-9' THEN 
      DO:
         iBenAcct = SUBSTR (iBenAcct,1,vBenLen).
         {key-tst.i iBenAcct ivmfo icorracct "/*" " " " WITH FRAME edit"}
      END.
   END.
END PROCEDURE.

/* Сработает только в тех полях, которых нет в {&lookup} */
ON 'F1':U OF FRAME Edit ANYWHERE DO:
   { realtrig.i &name = LABEL }
END.

PROCEDURE OnEntry_Op.Details:
   DEFINE VARIABLE t-details LIKE op.details NO-UNDO.

   t-details = SELF:SCREEN-VALUE.
   RUN p-detail.p (RECID (op),INPUT-OUTPUT t-details).
   SELF:SCREEN-VALUE = t-details.

END PROCEDURE.

{opdetail.i Edit}

ON "F6" ANYWHERE DO:

   IF TRANSACTION AND FRAME-NAME = "edit" THEN DO TRANSACTION:
      MESSAGE "Изменить роль банка?"
      VIEW-AS ALERT-BOX QUESTION BUTTONS YES-NO
      SET opreg_choice AS LOGICAL.

      IF NOT opreg_choice OR opreg_choice EQ ? THEN
         RETURN "NO-APPLY".

      IF op.doc-kind = "rec" OR op.doc-kind = "" THEN
         ASSIGN
            op.doc-kind = "send"
            dockind    = {&send-label}
         .
      ELSE
         ASSIGN
            op.doc-kind = "rec"
            dockind    = {&rec-label}
         .
      DISPLAY dockind WITH FRAME edit.
      RETURN NO-APPLY.
   END.
END.

h_frame = FRAME edit:HANDLE.
ON ENTRY OF FRAME edit DO:
    RUN GetFrameFields IN h_cmp (FRAME edit:HANDLE).
END.

ON "GO":U,"ctrl-g":U OF FRAME edit ANYWHERE DO:

   DEFINE VARIABLE wh      AS WIDGET-HANDLE NO-UNDO.
   DEFINE VARIABLE vOk     AS LOGICAL       NO-UNDO.
   DEFINE VARIABLE vResult AS INT64       NO-UNDO.
   DEFINE VARIABLE vMsg    AS CHARACTER     NO-UNDO.




/*********************************
 * Запрет правки документов из РКЦ
 * В теле документа ничего нельзя
 * править.
 *********************************/

IF LOGICAL(FGetSetting("PirChkop","DenyEditAuto","TRUE")) THEN DO:
 IF op.user-id EQ "MCI" OR op.user-id EQ "BNK-CL" THEN DO:

	   MESSAGE COLOR WHITE/RED "ОШИБКА! НЕЛЬЗЯ ПРАВИТЬ ОТ ИСПОЛНИТЕЛЯ " + op.user-id + "!!!" 
	   VIEW-AS ALERT-BOX ERROR TITLE "Ошибка документа #692".
	   RETURN NO-APPLY.

 END. /* IF user-id */
END. /* IF FGetSetting */

/*
   IF op.doc-type:MODIFIED
      OR op.doc-num:MODIFIED
      OR op.doc-date:MODIFIED
      OR op.due-date:MODIFIED
      OR op.op-value-date:MODIFIED
      OR op.order-pay:MODIFIED
      OR op.contract-date:MODIFIED
      OR op.ins-date:MODIFIED 
      OR op.name-ben:MODIFIED 
      OR op.inn:MODIFIED 
      OR op.details:MODIFIED
      OR vcorr-acct:MODIFIED
      OR vmfo:MODIFIED
THEN DO:

   END.
*/


   RUN CheckBenAcct (            op.ben-acct:SCREEN-VAL,
                     INPUT INPUT vmfo, 
                     INPUT INPUT vcorr-acct, 
                     OUTPUT      vOk).
   IF NOT vok THEN 
   DO:
      APPLY "entry" TO op.ben-acct IN FRAME edit.
      RETURN NO-APPLY.
   END.
   RUN CheckFields in h_xclass (FRAME edit:HANDLE, "",op.class-code,OUTPUT wh).

   IF RETURN-VALUE <> "" THEN 
   DO:
      APPLY "entry" TO wh.
      RETURN NO-APPLY.
   END.

   IF INPUT vmfo EQ "" THEN
   DO:
      IF mBankNameOrig EQ "" THEN
         vcorr-acct:SCREEN-VALUE = "".
   END.
   ELSE IF NOT g-checkbank(INPUT INPUT vmfo,
                           mBankCodeType,
                           INPUT INPUT vcorr-acct, 
                           INPUT INPUT op.ben-acct, 
                           OUTPUT vResult, 
                           OUTPUT vMsg) THEN
   DO:
      CASE vResult:
         WHEN {&EGMissingMFO} OR WHEN {&EGMissingBank} THEN
         DO:
            MESSAGE SKIP vMsg SKIP(1) VIEW-AS ALERT-BOX ERROR BUTTONS OK.
            APPLY "entry" TO vmfo.
         END.
         WHEN {&EGMissingCorrAcct} OR WHEN {&EGBadCorrAcct} THEN 
            IF mBankCodeType EQ "МФО-9" OR mBankCodeType EQ "" THEN
            DO:
               MESSAGE SKIP vMsg SKIP(1) VIEW-AS ALERT-BOX ERROR BUTTONS OK.
               APPLY "entry" TO vcorr-acct.
            END.
            ELSE
               RETURN.
         OTHERWISE 
            RETURN.
      END CASE.
      RETURN NO-APPLY.
   END.
   IF INPUT vmfo <> "" THEN DO:
      {getbank.i bank1 "INPUT vmfo" mBankCodeType}
      RUN Check-Bank IN h_op (BUFFER bank1,TRUE, OUTPUT flag-error).
      IF NOT flag-error EQ 0 THEN 
         RETURN NO-APPLY.
   END.

   IF Chksgnopint(INPUT op.op) THEN DO:
      {message1
          &text    =  "Документ создан транзакцией "" + STRING(op.op-kind) +  "",|продолжить изменение (сумма документа может 
          |не совпасть с суммой комиссии по счету 47423)?".
          &buttons = YES-NO}
      IF pick-value EQ "NO" THEN  RETURN NO-APPLY.
   END.

   PAUSE 0.
   RETURN.
END.

ON 'LEAVE':U OF op.ben-acct IN FRAME edit DO:

   DEFINE VARIABLE vOk AS LOGICAL    NO-UNDO.

   RUN CheckBenAcct (SELF:SCREEN-VAL,INPUT INPUT vmfo, INPUT INPUT vcorr-acct,OUTPUT vOk).

   IF NOT vOk
      THEN RETURN NO-APPLY.
      ELSE RETURN.
END.

ON 'LEAVE':U OF op.order-pay IN FRAME edit DO:

   IF NOT CAN-FIND(FIRST code WHERE code.class EQ "order-pay" AND code.CODE EQ SELF:SCREEN-VAL) THEN DO:
      &IF DEFINED(SESSION-REMOTE) &THEN
      MESSAGE "Нет такого кода очередности платежа" VIEW-AS ALERT-BOX.
      &ELSE
      MESSAGE "Нет такого кода очередности платежа".
      &ENDIF      
      RETURN NO-APPLY.
   END.
END.

ON "F1":U OF op.name-ben
DO:
   IF NOT CAN-FIND(FIRST op-kind OF op WHERE op-kind.proc EQ "g-midlds") THEN DO:
   /* если документ порожден процедурой не g-midlds */
      RUN "cli-r(ot.p" ("", 1, 4).
      IF KEYFUNCTION(LASTKEY) NE "END-ERROR" THEN DO:
         IF pick-value NE ? THEN DO:
            IF pick-value BEGINS "Ю," THEN DO:
               FIND cust-corp WHERE cust-corp.cust-id = INT64(ENTRY(2, pick-value)) NO-LOCK.
            END.
            ELSE IF pick-value BEGINS "Ч," THEN do:
               FIND person WHERE person.person-id = INT64(ENTRY(2, pick-value)) NO-LOCK.
            END.
            ELSE IF pick-value BEGINS "Б," THEN DO:
               FIND banks WHERE banks.bank-id = INT64(ENTRY(2, pick-value)) NO-LOCK.
            END.
         END.
      END.
      IF AVAIL cust-corp THEN DO:
         DISPLAY cust-corp.inn @ op.inn
                 cust-corp.cust-stat + " " + cust-corp.name-corp @ op.name-ben
         WITH FRAME edit.
      END.
      ELSE IF AVAIL person THEN DO:
         DISPLAY person.name-last + " " + person.first-name @ op.name-ben
                 person.inn @ op.inn
         WITH FRAME edit.
      END.
      ELSE IF AVAIL banks THEN DO:
         RUN GetCustIdent("Б", banks.bank-id, ?, ?, "ИНН", OUTPUT op.inn).
         DISPLAY banks.name @ op.name-ben
                 op.inn
         WITH FRAME edit.
      END.
   END.
   ELSE DO:
      pick-value = ?.
      IF INPUT vMFO NE "" THEN DO:
         IF INPUT op.ben-acct NE "" THEN
            RUN cli-ds.p ("", 1,
                          INPUT STRING(INT64(INPUT vMFO), "999999999") +
                          "," + INPUT op.ben-acct, Level + 1).
         ELSE 
            RUN cli-ds.p ("", 1, INPUT STRING(INT64(INPUT vMFO), "999999999"),  Level + 1).
      END.
      ELSE DO:
         RUN cli-ds.p ("", 1, "",  Level + 1).
      END.

      FOR FIRST code WHERE code.class EQ "recipient" AND code.code = pick-value NO-LOCK:
         DISPLAY
            code.name @ op.name-ben
            code.val  @ op.inn
         WITH FRAME edit.
      END.
   END.

   RETURN.
END.

RUN "g-func.p" PERSISTENT SET hproc.

DEFINE VARIABLE vChClassChilds AS CHARACTER NO-UNDO.
DEFINE VARIABLE vDocTypeDig    AS CHARACTER NO-UNDO.
DEFINE VARIABLE vOrderPay      AS INT64   NO-UNDO.
DEFINE VARIABLE vCustNameLine  AS CHARACTER NO-UNDO.

                        /* Проверка прав доступа к объектам подчиненных */
IF     in-user-id NE USERID ("bisquit")
   AND NOT GetSlavePermission (USERID ("bisquit"), in-user-id, "w")
THEN RETURN.

vChClassChilds = GetXclassAllChilds("opv").

FIND FIRST op WHERE RECID(op) EQ in-rec-id NO-LOCK NO-ERROR.
IF CheckSrc(op.class-code,op.op) OR CheckTrg(op.class-code,op.op,mTargetId) THEN DO:
   RUN Fill-SysMes IN h_tmess ("", "", "", "ВНИМАНИЕ! Редактирование документа невозможно. Есть связанный входящий документ в другом филиале.").
   RETURN.
END.
/*****************
 * Запреты на исправление документов
 * PIR
 *****************/

 /*** Линкованные документы ***/
{pir-op-ed-1.i}
 /*** Автоматизированный функционал ***/
{pir-op-ed-2.i}
/*** Запрет правки документов из РКЦ ***/
{pir-op-ed-3.i}

RUN GetDocTypeDigital IN h_op (op.doc-type, ?, OUTPUT vDocTypeDig).
vOrderPay = INT64(op.order-pay) NO-ERROR.
IF  vDocTypeDig <> "02"
AND vDocTypeDig <> "06"
AND (       (vOrderPay = ?)
     OR NOT (vOrderPay >= 1 AND vOrderPay <= 3))
   THEN FOR EACH op-entry OF op NO-LOCK:
   RELEASE acct.
   {find-act.i
      &bact = acct
      &acct = op-entry.acct-db
      &curr = op-entry.currency
   }
   IF AVAILABLE acct THEN DO:
      RUN chk-blk.p (acct.cust-cat,acct.cust-id).
      IF RETURN-VALUE EQ "0" THEN DO:
         {getcustline.i &cust-cat = "acct.cust-cat" &cust-id = "acct.cust-id" &output-to = "vCustNameLine"}
         RUN Fill-SysMes IN h_tmess ("", "acct43", "", "%s=" + vCustNameLine + "%s=" + STRING(acct.number,GetAcctFmt(acct.acct-cat))).
         RETURN.
      END.
   END.
END.

IF AVAILABLE op THEN
   RELEASE op.
RUN SetSysConf IN h_base ("PROCESS_OP-EDIT","Да").
{rec-ed.i
  &file=op
  &ef="op#.lf"
  &nocols=Yes
  &preset="op#.fnd "
  &editing="op.ed "
  &lookup="op.nau "
  &update="op#.upd "
    &undo="undo outr, retry outr"
  &access-class=op.class-code
  &access-surr=string(op)
}
RUN SetSysConf IN h_base ("PROCESS_OP-EDIT",?).
DELETE PROCEDURE(hproc).

{intrface.del}
