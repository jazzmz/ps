{pirsavelog.p}
/*
		Процедура экспорта данных для мастера подготовки депозитного договора
		Бурягин Е.П., 21.03.2007 13:47
*/
FUNCTION cnv RETURNS CHARACTER (INPUT arg1 AS CHARACTER).

/* Вход: arg1:строка
** Выход: строка в кодировке windows-1251
*/
	RETURN CODEPAGE-CONVERT(arg1,"1251",SESSION:CHARSET).

END FUNCTION.

FUNCTION expagr RETURNS CHARACTER (
																	INPUT arg1 AS CHAR, /* Номер договора */
																	INPUT arg2 AS CHAR, /* Дата открытия */
																	INPUT arg3 AS CHAR, /* Зата окончания */
																	INPUT arg4 AS CHAR, /* Сумма */
																	INPUT arg5 AS CHAR, /* Валюта */
																	INPUT arg6 AS CHAR  /* Процентная ставка */ 
																  ).

	DEFINE VARIABLE construction AS CHARACTER.

	construction =                "<agreement>" + CHR(13) + CHR(10).
	construction = construction + "no=" + arg1 + CHR(13) + CHR(10).
	construction = construction + "opendate=" + arg2 + CHR(13) + CHR(10).
	construction = construction + "enddate=" + arg3 + CHR(13) + CHR(10).
   construction = construction + "duration=" + STRING(DATE(arg3) - DATE(arg2) + 1) + CHR(13) + CHR(10).
	construction = construction + "summa=" + arg4 + CHR(13) + CHR(10).
	construction = construction + "currency=" + arg5 + CHR(13) + CHR(10).
	construction = construction + "rate=" + arg6 + CHR(13) + CHR(10).
   construction = construction + "</agreement>" + CHR(13) + CHR(10).
	
	RETURN construction.

END FUNCTION.

FUNCTION expacct RETURNS CHARACTER (INPUT arg1 AS CHAR,
																		INPUT arg2 AS CHAR).

	DEFINE VARIABLE construction AS CHARACTER.

	construction =                "<acct>" + CHR(13) + CHR(10).
	construction = construction + "no=" + arg1 + CHR(13) + CHR(10).
	if can-do("Расчет",arg2) then 
		construction = construction + "type=расчетный" + CHR(13) + CHR(10).
	if can-do("Текущ*",arg2) then 
		construction = construction + "type=текущий валютный" + CHR(13) + CHR(10).
	if can-do("Депоз",arg2) then 
		construction = construction + "type=депозитный" + CHR(13) + CHR(10).
	construction = construction + "</acct>" + CHR(13) + CHR(10).
	
	RETURN construction.

END FUNCTION.


FUNCTION expcorp RETURNS CHARACTER (INPUT arg1 AS CHAR,
																		INPUT arg2 AS CHAR,
																		INPUT arg3 AS CHAR,
																		INPUT arg4 AS CHAR, 
																		INPUT arg5 AS CHAR).

	DEFINE VARIABLE construction AS CHARACTER.

	construction =                "<client>" + CHR(13) + CHR(10).
	construction = construction + "name=" + arg1 + CHR(13) + CHR(10).
	construction = construction + "address=" + arg2 + CHR(13) + CHR(10).
	construction = construction + "ogrn=" + arg3 + CHR(13) + CHR(10).
	construction = construction + "inn=" + arg4 + CHR(13) + CHR(10).
	construction = construction + "resident=" + arg5 + CHR(13) + CHR(10).
	construction = construction + "</client>" + CHR(13) + CHR(10).
	
	RETURN construction.

END FUNCTION.


FUNCTION expbos RETURNS CHARACTER (INPUT arg1 AS CHAR,
																		INPUT arg2 AS CHAR,
																		INPUT arg3 AS CHAR).

	DEFINE VARIABLE construction AS CHARACTER.

	construction =                "<bos>" + CHR(13) + CHR(10).
	construction = construction + "fio=" + arg1 + CHR(13) + CHR(10).
	construction = construction + "post=" + arg2 + CHR(13) + CHR(10).
	construction = construction + "order=" + arg3 + CHR(13) + CHR(10).
	construction = construction + "</bos>" + CHR(13) + CHR(10).
	
	RETURN construction.

END FUNCTION.

FUNCTION expaccounter RETURNS CHARACTER (INPUT arg1 AS CHAR).

	DEFINE VARIABLE construction AS CHARACTER.

	construction =                "<accounter>" + CHR(13) + CHR(10).
	construction = construction + "fio=" + arg1 + CHR(13) + CHR(10).
	construction = construction + "</accounter>" + CHR(13) + CHR(10).
	
	RETURN construction.

END FUNCTION.

/********************************************************************** VARS */

/* Глобальные определения */
{globals.i}
{ulib.i}
{tmprecid.def}        /** Используем информацию из броузера */

/* Входные параметры процедуры */
DEF INPUT PARAM iParam AS CHAR.

/* Первый параметр */
DEF VAR out_file_name AS CHAR. 
DEF VAR account AS CHAR.
DEF VAR summa AS DECIMAL. 
DEF VAR rate AS DECIMAL.

IF NUM-ENTRIES(iParam) > 0 THEN
	out_file_name = ENTRY(1,iParam).
ELSE
	out_file_name = "/home2/bis/quit41d/work/buryagin/tmp/new_data.afx".
		
OUTPUT TO VALUE(out_file_name).

PUT UNFORMATTED cnv("<data>" + CHR(13) + CHR(10)).

/* Для первого счета, выбранного в брoузере выполняем... */
FOR FIRST tmprecid 
         NO-LOCK,
   FIRST loan WHERE 
         RECID(loan) EQ tmprecid.id 
         NO-LOCK 
: 

	account = GetLoanAcct_ULL(loan.contract, loan.cont-code, 'Депоз', loan.open-date, false).
	/** Найдем сумму договора */
	/** Если как остаток по счету, то...
		summa = ABS(GetAcctPosValueEx_UAL(account, loan.currency, loan.open-date, "Ф", false)).
	*/
	/** Если из условия, то воспользуемся функцией GetLoanLimit_ULL(). Не смотря на ее название,
	    она для депозитных договоров вернет именно сумму договора на дату 
	*/
	
	summa = GetLoanLimit_ULL(loan.contract, loan.cont-code, loan.open-date, false).
		
	rate = GetLoanCommission_ULL(loan.contract, loan.cont-code, '%Деп', loan.open-date, false).
	
	PUT UNFORMATTED cnv(expagr(loan.cont-code, 
				STRING(loan.open-date,"99/99/9999"), 
				STRING(loan.end-date,"99/99/9999"),
				STRING(summa),
				SUBSTRING(account, 6, 3),
				STRING(rate * 100))).
				
	PUT UNFORMATTED cnv(expacct(account, "Депоз")).
	
	account = GetLoanAcct_ULL(loan.contract, loan.cont-code, 'ДепРасч', loan.open-date, false).
	/** Найдем назначение счета */
	FIND FIRST acct WHERE acct.acct = account NO-LOCK NO-ERROR.
	IF AVAIL acct THEN DO:
		PUT UNFORMATTED cnv(expacct(account, acct.contract)).
	END.
					
	/* Информация по клиенту */
	IF loan.cust-cat = "Ю" THEN
		DO:
			FIND FIRST cust-corp WHERE 
					cust-corp.cust-id = loan.cust-id NO-LOCK.
			PUT UNFORMATTED cnv(expcorp(GetXAttrValue("cust-corp", STRING(cust-corp.cust-id), "FullName"),
						    cust-corp.addr-of-low[1] /* + " " + cust-corp.addr-of-low[2] */,
						    GetXAttrValueEx("cust-corp", STRING(cust-corp.cust-id), "ОГРН", ""),
						    cust-corp.inn,
						    (IF cust-corp.country-id = "RUS" THEN "yes" ELSE "no")
						)).

			/** Информация о руководителе */
			PUT UNFORMATTED cnv(expbos(GetXAttrValueEx("cust-corp", STRING(cust-corp.cust-id), "ФИОрук", ""),
																 GetXAttrValueEx("cust-corp", STRING(cust-corp.cust-id), "ДолРук", ""),
																 "")).

		END.
	ELSE
		DO:
			MESSAGE "Счет не принадлежит клиенту юридическому лицу!" VIEW-AS ALERT-BOX.
			RETURN.
		END.
	
	
END.



PUT UNFORMATTED cnv("</data>" + CHR(13) + CHR(10)).

OUTPUT CLOSE.

MESSAGE "Данные успешно экспортированы!" VIEW-AS ALERT-BOX.